file          -> stmt* EOF

stmt          -> exprStmt
               | printStmt
               | assignStmt
               | ifStmt

exprStmt      -> expr "\n"
printStmt     -> "print" "(" expr ")" "\n"
assignStmt    -> IDENTIFIER "=" expr "\n"
ifStmt        -> "if" expr ":" block ("else" block)?

block         -> "\n" INDENT stmt* DEDENT

expr          -> equality
equality      -> comparison (("==" | "!=") comparison)*
comparison    -> term ((">" | ">=" | "<" | "<=") term)*
term          -> factor (("+" | "-") factor)*
factor        -> unary (("*" | "/") unary)*
unary         -> ("-" | "not") unary
               | primary
primary       -> NUMBER | STRING
               | "True" | "False" | "None"
               | "(" expr ")"
               | IDENTIFIER
