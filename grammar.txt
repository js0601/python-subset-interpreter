file          -> stmt* EOF

stmt          -> exprStmt
               | printStmt
               | assignVarStmt
               | assignLsStmt
               | ifStmt
               | whileStmt
               | funDecl
               | returnStmt

exprStmt      -> expr "\n"
printStmt     -> "print" "(" expr ")" "\n"
assignVarStmt -> IDENTIFIER "=" expr "\n"
assignLsStmt  -> IDENTIFIER "[" expr "]" "=" expr "\n"
ifStmt        -> "if" expr ":" block ("else" ":" block)?
whileStmt     -> "while" expr ":" block
funDecl       -> "def" IDENTIFIER "(" parameters? ")" ":" block
returnStmt    -> "return" expr?

block         -> "\n" INDENT stmt* DEDENT
parameters    -> IDENTIFIER ("," IDENTIFIER)*

expr          -> disjunction
disjunction   -> conjunction ("or" conjuction)*
conjunction   -> equality ("and" equality)*
equality      -> comparison (("==" | "!=") comparison)*
comparison    -> term ((">" | ">=" | "<" | "<=") term)*
term          -> factor (("+" | "-") factor)*
factor        -> unary (("*" | "/") unary)*
unary         -> ("-" | "not") unary
               | primary
primary       -> NUMBER | STRING
               | "True" | "False" | "None"
               | "[" arguments? "]"
               | "(" expr ")"
               | IDENTIFIER ("(" arguments? ")" | "[" expr "]")?

arguments     -> expr ("," expr)*
