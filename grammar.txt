file          -> stmt* EOF

stmt          -> exprStmt
               | printStmt
               | assignStmt
               | ifStmt
               | whileStmt
               | funDecl

exprStmt      -> expr "\n"
printStmt     -> "print" "(" expr ")" "\n"
assignStmt    -> IDENTIFIER "=" expr "\n"
ifStmt        -> "if" expr ":" block ("else" ":" block)?
whileStmt     -> "while" expr ":" block
funDecl       -> "def" IDENTIFIER "(" parameters? ")" ":" block

block         -> "\n" INDENT stmt* DEDENT
parameters    -> IDENTIFIER ("," IDENTIFIER)*

expr          -> disjunction
disjunction   -> conjunction ("or" conjuction)*
conjunction   -> equality ("and" equality)*
equality      -> comparison (("==" | "!=") comparison)*
comparison    -> term ((">" | ">=" | "<" | "<=") term)*
term          -> factor (("+" | "-") factor)*
factor        -> unary (("*" | "/") unary)*
unary         -> ("-" | "not") unary
               | primary
primary       -> NUMBER | STRING
               | "True" | "False" | "None"
               | "(" expr ")"
               | IDENTIFIER ("(" arguments? ")")?

arguments     -> expr ("," expr)*
